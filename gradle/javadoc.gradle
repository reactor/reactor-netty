/*
 * Copyright (c) 2020-2023 VMware, Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
if (project.name == 'reactor-netty-examples' || project.name == 'reactor-netty-graalvm-smoke-tests') {
	return
}

project.tasks.withType(Javadoc) {
	dependsOn jar
	group = "Reactor Netty Javadoc"

	description = "Generates aggregated Javadoc API documentation."
	title = "${project.name} ${version}"

	compileTestJava.options.compilerArgs += "-parameters"
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}

	options.addStringOption('charSet', 'UTF-8')

	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.header = "${project.name}"
	options.overview = "$rootDir/docs/api/overview.html"
	options.stylesheetFile = file("$rootDir/docs/api/stylesheet.css")
	options.links(rootProject.ext.javadocLinks)

	maxMemory = "1024m"
	destinationDir = new File(buildDir, "docs/javadoc")
	source sourceSets.main.allJava
	doFirst {
		classpath = files(sourceSets.main.compileClasspath)
	}
}

