/*
 * Copyright (c) 2011-2018 Pivotal Software Inc, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.gradle.api.internal.plugins.osgi.OsgiHelper

buildscript {
  repositories {
	maven { url "http://repo.spring.io/plugins-release" }
  }
  dependencies {
	classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7',
			'io.spring.gradle:spring-io-plugin:0.0.4.RELEASE',
			'com.github.jengelman.gradle.plugins:shadow:1.2.0'
  }
}

plugins {
  id "com.github.hierynomus.license" version "0.14.0"
}

description = 'Reactive Streams Netty driver'

ext {
  if (project.hasProperty('versionBranch') && version.toString().endsWith(".BUILD-SNAPSHOT")) {
	versionBranch = versionBranch.replaceAll("\"", "").trim()
	if (!versionBranch.isEmpty()) {
	  realVersion = version.toString().replace("BUILD-SNAPSHOT", versionBranch + ".BUILD-SNAPSHOT")
	  project.version = realVersion
	  println "Building special snapshot ${project.version}"
	  println "OSGI version would be: ${new OsgiHelper().getVersion(project.version.toString())}"
	}
  }

  gradleVersion = '3.3'
  gradleScriptDir = "${rootProject.projectDir}/gradle"

  reactorCoreVersion = "3.1.12.BUILD-SNAPSHOT"

  // Languages
  groovyVersion = '2.4.1'

  // Logging
  slf4jVersion = '1.7.12'
  logbackVersion = '1.1.2'
  testAddonVersion = reactorCoreVersion
  assertJVersion = '3.6.1'

  // Libraries
  nettyVersion = '4.1.31.Final'
  jacksonDatabindVersion = '2.5.1'

  // Testing
  mockitoVersion = '1.10.19'

  javadocLinks = ["http://docs.oracle.com/javase/7/docs/api/",
				  "http://docs.oracle.com/javaee/6/api/",
				  "http://fasterxml.github.io/jackson-databind/javadoc/2.5/",
				  "http://www.reactive-streams.org/reactive-streams-1.0.2-javadoc/",
				  "http://projectreactor.io/docs/core/release/api/",
				  "http://netty.io/4.1/api/",
				  "http://projectreactor.io/docs/netty/release/api/",] as String[]
}


configure(rootProject) { project ->
  group = 'io.projectreactor.ipc'

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply from: "$gradleScriptDir/setup.gradle"
  apply plugin: 'propdeps'
  apply plugin: 'osgi'
  apply from: "${gradleScriptDir}/ide.gradle"
  apply from: "${gradleScriptDir}/errorprone.gradle"

  jacoco {
	toolVersion = '0.7.7.201606060606'
  }

  jacocoTestReport {
	reports {
	  xml.enabled = true
	  html.enabled = true
	}
  }

  ext.bundleImportPackages = [ '!javax.annotation',
							   'io.netty.channel.kqueue;resolution:=optional;version="[4.1,5)"',
							   '*']

  [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:varargs",
														   "-Xlint:cast",
														   "-Xlint:classfile",
														   "-Xlint:dep-ann",
														   "-Xlint:divzero",
														   "-Xlint:empty",
														   "-Xlint:finally",
														   "-Xlint:overrides",
														   "-Xlint:path",
														   "-Xlint:processing",
														   "-Xlint:static",
														   "-Xlint:try",
														   "-Xlint:deprecation",
														   "-Xlint:unchecked",
														   "-Xlint:-serial",      // intentionally disabled
														   "-Xlint:-options",     // intentionally disabled
														   "-Xlint:-fallthrough", // intentionally disabled
														   "-Xlint:rawtypes"
  ]

  compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  if (JavaVersion.current().isJava8Compatible()) {
	compileTestJava.options.compilerArgs += "-parameters"
	tasks.withType(Javadoc) {
	  options.addStringOption('Xdoclint:none', '-quiet')
	  options.addStringOption('encoding', 'UTF-8')
	}
  }

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  sourceSets.test.resources.srcDirs = ["src/test/resources", "src/test/java"]

  configurations.all {
	exclude group: 'commons-logging', module: 'commons-logging'
  }

  project.tasks.withType(Test).all {
	// run tests with IPv4 only when IPv6 is available
	if (project.hasProperty('preferIPv4Stack')) {
		systemProperty("java.net.preferIPv4Stack", "true")
	}
	// run tests with preferring IPv6 addresses
	if (project.hasProperty('preferIPv6Addresses')) {
		systemProperty("java.net.preferIPv6Addresses", "true")
	}
	systemProperty("java.awt.headless", "true")
	systemProperty("reactor.trace.cancel", "true")
	systemProperty("reactor.trace.nocapacity", "true")
	systemProperty("testGroups", project.properties.get("testGroups"))
	scanForTestClasses = false
	include '**/*Tests.*'
	include '**/*Test.*'
	include '**/*Spec.*'
	exclude '**/*Abstract*.*'
  }

  test {
	testLogging {
	  events  "passed", "failed"
	  showExceptions true
	  exceptionFormat "full"
	  maxGranularity 3
	}

	onOutput { descriptor, event ->
	  if (event.message.contains("ResourceLeakDetector")) {
		logger.error("ERROR: Test: " + descriptor + " produced resource leak: " + event.message )
	  }
	}
  }

  repositories {
	if (version.endsWith('BUILD-SNAPSHOT') || project.hasProperty('platformVersion')) {
	  if (System.getenv ()["bamboo_buildNumber"] == null) {
		mavenLocal()
	  } else {
		println 'Bamboo CI detected, avoiding use of mavenLocal()'
	  }
	  maven { url 'http://repo.spring.io/libs-snapshot' }
	}
	maven { url 'http://repo.spring.io/libs-milestone' }
	maven { url 'http://repo.spring.io/libs-release' }
	maven { url "https://oss.sonatype.org/content/repositories/releases/" }
	jcenter()
	mavenCentral()
  }

  // dependencies that are common across all java projects
  dependencies {
	// JSR-305 annotations
	compileOnly "com.google.code.findbugs:jsr305:3.0.2"

	// Logging
	optional "org.slf4j:slf4j-api:$slf4jVersion"

	compile "io.projectreactor:reactor-core:$reactorCoreVersion"

	//Default reactor-netty impl
	//compile "io.netty:netty-all:$nettyVersion"

	compile "io.netty:netty-handler:${nettyVersion}"
	compile "io.netty:netty-handler-proxy:${nettyVersion}"
	compile "io.netty:netty-codec-http:${nettyVersion}"
	compile "io.netty:netty-transport-native-epoll:${nettyVersion}:linux-x86_64"
	optional "io.netty:netty-transport-native-kqueue:${nettyVersion}"

	// Testing
	testCompile "org.apache.httpcomponents:fluent-hc:4.3.6"
	testCompile "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
	testCompile "io.projectreactor:reactor-test:$testAddonVersion"

	testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"

	testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	// Testing
	testCompile "org.hamcrest:hamcrest-library:1.3",
			"org.assertj:assertj-core:$assertJVersion",
			"org.testng:testng:6.8.5"
  }


  if (project.hasProperty('platformVersion')) {
	apply plugin: 'spring-io'

	repositories {
	  maven { url 'http://repo.spring.io/libs-snapshot' }
	}

	dependencyManagement {
	  springIoTestRuntime {
		imports {
		  mavenBom "io.spring.platform:platform-bom:$platformVersion"
		}
	  }
	}
  }


  jar {
	manifest {
	  instruction 'Import-Package', bundleImportPackages.join(',')
	  attributes("Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
			  "Implementation-Title": project.name,
			  "Implementation-Version": project.version)
	}
  }

  check.dependsOn jacocoTestReport
}


configurations.all {
  // check for updates every build
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'

  license {
    header rootProject.file('codequality/HEADER')
    includes(["**/*.java", "**/*.css"])
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
        css = 'SLASHSTAR_STYLE'
    }
  }
}
