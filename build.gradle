/*
 * Copyright (c) 2011-2021 VMware, Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import aQute.bnd.gradle.BundleTaskConvention
import me.champeau.gradle.japicmp.JapicmpTask

buildscript {
  repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/plugins-release" }
  }
  dependencies {
	classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7',
			'com.google.gradle:osdetector-gradle-plugin:1.7.0',
			'biz.aQute.bnd:biz.aQute.bnd.gradle:4.2.0',
			'org.jfrog.buildinfo:build-info-extractor-gradle:4.24.9' //applied in individual submodules
  }
}

plugins {
  id "com.diffplug.spotless" version "5.14.0"
  id 'org.asciidoctor.convert' version '1.5.11'
  id 'org.gradle.test-retry' version '1.3.1'
  id 'com.github.johnrengelman.shadow' version '5.2.0'
  //we now need version of Artifactory gradle plugin deployed on Maven Central, see above
  id "de.undercouch.download" version "4.1.1"
  id 'me.champeau.gradle.japicmp' version '0.2.9'
  id 'io.spring.nohttp' version '0.0.5.RELEASE'
}

description = 'Reactive Streams Netty driver'

ext {
  if (project.hasProperty('versionBranch') && version.toString().endsWith(".BUILD-SNAPSHOT")) {
	versionBranch = versionBranch.replaceAll("\"", "").trim()
	if (!versionBranch.isEmpty()) {
	  realVersion = version.toString().replace("BUILD-SNAPSHOT", versionBranch + ".BUILD-SNAPSHOT")
	  project.version = realVersion
	  println "Building special snapshot ${project.version}"
	}
  }

  gradleScriptDir = "${rootProject.projectDir}/gradle"

  //Metrics
  micrometerVersion = '1.3.0' //optional baseline: technically could work with 1.2.x, should work with any 1.3.x

  jsr305Version = '3.0.2'

  // Logging
  slf4jVersion = '1.7.31'
  logbackVersion = '1.2.3'

  // Netty
  nettyDefaultVersion = '4.1.65.Final'
  if (!project.hasProperty("forceNettyVersion")) {
	nettyVersion = nettyDefaultVersion
  }
  else {
	nettyVersion = forceNettyVersion
	println "Netty version defined from command line: ${forceNettyVersion}"
  }

  // Testing
  jacksonDatabindVersion = '2.12.3'
  testAddonVersion = reactorCoreVersion
  assertJVersion = '3.20.2'
  hoverflyJavaVersion = '0.14.0'
  tomcatVersion = '9.0.48'
  boringSslVersion = '2.0.38.Final'
  junitVersion = '5.7.2'

  javadocLinks = ["https://docs.oracle.com/javase/7/docs/api/",
				  "https://docs.oracle.com/javaee/6/api/",
				  "https://fasterxml.github.io/jackson-databind/javadoc/2.5/",
				  "https://www.reactive-streams.org/reactive-streams-1.0.3-javadoc/",
				  "https://projectreactor.io/docs/core/release/api/",
				  "https://netty.io/4.1/api/",
				  "https://projectreactor.io/docs/netty/release/api/",] as String[]

	bndOptions = [
			"Export-Package": "!reactor.netty.internal*,reactor.netty.*;-noimport:=true",
			"Import-Package": '!javax.annotation,io.netty.channel.kqueue;resolution:=optional;version="[4.1,5)",io.netty.handler.codec.haproxy;resolution:=optional;version="[4.1,5)",io.micrometer.*;resolution:=optional,*',
			"Bundle-Name" : "reactor-netty",
			"Bundle-SymbolicName" : "io.projectreactor.netty.reactor-netty"
	]
}

nohttp {
	source.exclude "src/docs/asciidoc/highlight/**"
	source.exclude "**/build/**"
}

spotless {
	if (project.hasProperty("spotlessFrom")) {
		if (project.spotlessFrom == "ALL") {
			println "[Spotless] Ratchet deactivated"
		}
		else {
			println "[Spotless] Ratchet from $project.spotlessFrom"
			ratchetFrom project.spotlessFrom
		}
	}
	else if (System.getenv ()["GITHUB_ACTION"] != null) {
		println "[Spotless] GitHub Action detected without explicit branch, not enforcing check"
		enforceCheck false
	}
	else {
		String spotlessBranch = "origin/0.9.x"
		println "[Spotless] Local run detected, ratchet from $spotlessBranch"
		ratchetFrom spotlessBranch
	}
	java {
		target '**/*.java'
		licenseHeaderFile('codequality/spotless/licenseSlashstarStyle.txt')
	}
}

configure(rootProject) { project ->
  group = 'io.projectreactor.netty'

  apply plugin: 'java'
  apply plugin: 'jacoco'
  apply from: "$gradleScriptDir/releaser.gradle"
  apply from: "$gradleScriptDir/setup.gradle"
  apply plugin: 'propdeps'
  apply plugin: "biz.aQute.bnd.builder"
  apply from: "${gradleScriptDir}/errorprone.gradle"
  apply plugin: 'com.google.osdetector'
  apply plugin: 'idea'

  jacoco {
	toolVersion = '0.8.5'
  }

  jacocoTestReport {
	reports {
	  xml.enabled = true
	  html.enabled = true
	}
  }

  [compileJava, compileTestJava]*.options*.compilerArgs = ["-Xlint:varargs",
														   "-Xlint:cast",
														   "-Xlint:classfile",
														   "-Xlint:dep-ann",
														   "-Xlint:divzero",
														   "-Xlint:empty",
														   "-Xlint:finally",
														   "-Xlint:overrides",
														   "-Xlint:path",
														   "-Xlint:processing",
														   "-Xlint:static",
														   "-Xlint:try",
														   "-Xlint:deprecation",
														   "-Xlint:unchecked",
														   "-Xlint:-serial",      // intentionally disabled
														   "-Xlint:-options",     // intentionally disabled
														   "-Xlint:-fallthrough", // intentionally disabled
														   "-Xlint:rawtypes"
  ]

  compileJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  compileTestJava {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
  }

  if (JavaVersion.current().isJava8Compatible()) {
	compileTestJava.options.compilerArgs += "-parameters"
	tasks.withType(Javadoc) {
	  options.addStringOption('Xdoclint:none', '-quiet')
	  options.addStringOption('encoding', 'UTF-8')
	}
  }

  [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  sourceSets.test.resources.srcDirs = ["src/test/resources", "src/test/java"]

  configurations.all {
	exclude group: 'commons-logging', module: 'commons-logging'
  }

  project.tasks.withType(Test).all {
	// run tests with IPv4 only when IPv6 is available
	if (project.hasProperty('preferIPv4Stack')) {
		systemProperty("java.net.preferIPv4Stack", "true")
	}
	// run tests with preferring IPv6 addresses
	if (project.hasProperty('preferIPv6Addresses')) {
		systemProperty("java.net.preferIPv6Addresses", "true")
	}
	systemProperty("java.awt.headless", "true")
	systemProperty("reactor.trace.cancel", "true")
	systemProperty("reactor.trace.nocapacity", "true")
	systemProperty("testGroups", project.properties.get("testGroups"))
	systemProperty("io.netty.leakDetection.level", "paranoid")
	systemProperty("reactor.netty.pool.getPermitsSamplingRate", "0.5")
	systemProperty("reactor.netty.pool.returnPermitsSamplingRate", "0.5")
	scanForTestClasses = false
	include '**/*Tests.*'
	include '**/*Test.*'
	include '**/*Spec.*'
	exclude '**/*Abstract*.*'

	useJUnitPlatform()

	retry {
		maxFailures = 10
		maxRetries = 1
	}
  }

  test {
	testLogging {
	  events  "passed", "failed"
	  showExceptions true
	  exceptionFormat "full"
	  maxGranularity 3
	}

	onOutput { descriptor, event ->
	  if (event.message.contains("ResourceLeakDetector")) {
		logger.error("ERROR: Test: " + descriptor + " produced resource leak: " + event.message )
	  }
	}
  }

	sourceSets {
		jarFileTest
	}

	//TODO evaluate if this is still necessary, if not remove that and `idea` plugin
	idea.module.testSourceDirs += sourceSets.jarFileTest.allSource.srcDirs

	repositories {
	mavenCentral()
	maven { url "https://oss.sonatype.org/content/repositories/releases/" }
	maven { url 'https://repo.spring.io/milestone' }
	if (version.endsWith('BUILD-SNAPSHOT')) {
	  if (System.getenv ()["GITHUB_ACTION"] == null) {
		mavenLocal()
	  } else {
		println 'GitHub Action detected, avoiding use of mavenLocal()'
	  }
	  maven { url 'https://repo.spring.io/snapshot' }
	  maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}
  }

	def os_suffix = ""
	if (osdetector.classifier in ["linux-x86_64"] || ["osx-x86_64"] || ["windows-x86_64"]) {
		os_suffix = ":" + osdetector.classifier
	}

	configurations {
		shaded
	}

	// dependencies that are common across all java projects
  dependencies {
	// JSR-305 annotations
	compileOnly "com.google.code.findbugs:jsr305:$jsr305Version"

	compile "io.netty:netty-handler-proxy:$nettyVersion"
	compile "io.netty:netty-codec-http2:$nettyVersion"
	optional "io.netty:netty-codec-haproxy:$nettyVersion"

	//Metrics
	optional "io.micrometer:micrometer-core:$micrometerVersion"

	// Logging
	optional "org.slf4j:slf4j-api:$slf4jVersion"

	compile "io.projectreactor:reactor-core:$reactorCoreVersion"
	shaded("io.projectreactor.addons:reactor-pool:$reactorPoolVersion") {
		exclude module: "reactor-core"
	}

	compile "io.netty:netty-handler:$nettyVersion"
	compile "io.netty:netty-codec-http:$nettyVersion"

	//transport resolution: typical build forces epoll but not kqueue transitively
	//on the other hand, if we want to make transport-specific tests, we'll make all
	// native optional at compile time and add correct native/nio to testRuntime
	if (project.hasProperty("forceTransport")) {
	  //so that the main code compiles
	  optional "io.netty:netty-transport-native-epoll:$nettyVersion"
	  optional "io.netty:netty-transport-native-kqueue:$nettyVersion"
	  //now we explicitly add correctly qualified native, or do nothing if we want to test NIO
	  if (forceTransport == "native") {
		if (osdetector.os == "osx") {
		  testRuntime "io.netty:netty-transport-native-kqueue:$nettyVersion$os_suffix"
		}
		else if (osdetector.os == "linux") {
		  testRuntime "io.netty:netty-transport-native-epoll:$nettyVersion$os_suffix"
		}
	  }
	  else if (forceTransport != "nio") {
		throw new InvalidUserDataException("invalid -PforceTranport option " + forceTransport + ", should be native|nio")
	  }
	}
	else {
	  //classic build to be distributed
	  compile "io.netty:netty-transport-native-epoll:$nettyVersion:linux-x86_64"
	  optional "io.netty:netty-transport-native-kqueue:$nettyVersion"
	}

	// Testing
	testCompile "com.fasterxml.jackson.core:jackson-databind:$jacksonDatabindVersion"
	testCompile "io.projectreactor:reactor-test:$testAddonVersion"
	testCompile "org.assertj:assertj-core:$assertJVersion"
	testCompile "io.specto:hoverfly-java-junit5:$hoverflyJavaVersion"
	testCompile "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion"
	testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"

	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testRuntime "org.slf4j:jcl-over-slf4j:$slf4jVersion"
	testRuntime "ch.qos.logback:logback-classic:$logbackVersion"
	// Needed for HTTP/2 testing
	testRuntime "io.netty:netty-tcnative-boringssl-static:$boringSslVersion" + os_suffix

	jarFileTestCompile "org.assertj:assertj-core:$assertJVersion"
	jarFileTestCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
	jarFileTestRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

	for (dependency in project.configurations.shaded.dependencies) {
	    compileOnly(dependency)
	}
  }

  jar {
	classifier = 'original'

	manifest {
	  attributes("Created-By": "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})",
			  "Implementation-Title": project.name,
			  "Implementation-Version": project.version,
			  "Automatic-Module-Name": "reactor.netty")
	}
	bnd(bndOptions)
  }

  task downloadBaseline(type: Download) {
		onlyIf {
			if (project.gradle.startParameter.isOffline()) {
				println "Offline: skipping downloading of baseline and JAPICMP"
				return false
			}
			else if ("$compatibleVersion" == "SKIP") {
				println "SKIP: Instructed to skip the baseline comparison"
				return false
			}
			else {
				println "Will download and perform baseline comparison with ${compatibleVersion}"
				return true
			}
		}

		onlyIfNewer true
		compress true
		src "${repositories.mavenCentral().url}io/projectreactor/netty/reactor-netty/$compatibleVersion/reactor-netty-${compatibleVersion}-original.jar"
		dest "${buildDir}/baselineLibs/reactor-netty-${compatibleVersion}.jar"
  }

  task japicmp(type: JapicmpTask) {
		oldClasspath = files("${buildDir}/baselineLibs/reactor-netty-${compatibleVersion}.jar")
		newClasspath = files(jar.archiveFile)
		onlyBinaryIncompatibleModified = true
		failOnModification = true
		failOnSourceIncompatibility = true
		txtOutputFile = file("${project.buildDir}/reports/japi.txt")
		ignoreMissingClasses = true
		includeSynthetic = true
		methodExcludes = [
				// New methods with default implementation
				// reactor.netty.http.server
				"reactor.netty.http.server.HttpServerResponse#sendWebsocket(java.lang.String, int, java.util.function.BiFunction)",
				"reactor.netty.http.server.HttpServerResponse#sendWebsocket(boolean, java.util.function.BiFunction)",
				"reactor.netty.http.server.HttpServerResponse#sendWebsocket(java.lang.String, int, boolean, java.util.function.BiFunction)",
				"reactor.netty.http.server.HttpServerRoutes#ws(java.util.function.Predicate, java.util.function.BiFunction, java.lang.String, int, boolean)",
				"reactor.netty.http.server.HttpServerRoutes#ws(java.lang.String, java.util.function.BiFunction, reactor.netty.http.server.WebsocketServerSpec)",
				"reactor.netty.http.server.HttpServerRoutes#ws(java.util.function.Predicate, java.util.function.BiFunction, reactor.netty.http.server.WebsocketServerSpec)",
				// reactor.netty.resources
				"reactor.netty.resources.LoopResources#disposeLater(java.time.Duration, java.time.Duration)",
				"reactor.netty.resources.LoopResources#onChannel(java.lang.Class, io.netty.channel.EventLoopGroup)",
				"reactor.netty.resources.LoopResources#onChannelClass(java.lang.Class, io.netty.channel.EventLoopGroup)",

				// New static methods
				// reactor.netty.resources
				"reactor.netty.resources.ConnectionProvider#builder(java.lang.String)",
				"reactor.netty.resources.ConnectionProvider#create(java.lang.String)",
				"reactor.netty.resources.ConnectionProvider#create(java.lang.String, int)",
				"reactor.netty.resources.ConnectionProvider#elastic(java.lang.String, java.time.Duration, java.time.Duration)",
				"reactor.netty.resources.ConnectionProvider#fixed(java.lang.String, int, long, java.time.Duration, java.time.Duration)",
				"reactor.netty.resources.LoopResources#hasNativeSupport()",

				// Removed methods
				// reactor.netty.channel
				"reactor.netty.channel.BootstrapHandlers#updateMetricsSupport(io.netty.bootstrap.ServerBootstrap, java.lang.String, java.lang.String)",
				"reactor.netty.channel.BootstrapHandlers#updateMetricsSupport(io.netty.bootstrap.Bootstrap, java.lang.String, java.lang.String)",
				"reactor.netty.channel.MicrometerChannelMetricsRecorder#name()",
				"reactor.netty.channel.MicrometerChannelMetricsRecorder#registry()",
				"reactor.netty.channel.MicrometerChannelMetricsRecorder#remoteAddress()"
		]
		fieldExcludes = [
				// Removed fields
				// reactor.netty
				"reactor.netty.Metrics#MAX_URI_TAGS"
		]
		classExcludes = [
				// Parent class changed
				// reactor.netty.http
				"reactor.netty.http.MicrometerHttpMetricsRecorder",

				// Removed classes
				// reactor.netty.http.server
				"reactor.netty.http.server.HttpToH2Operations",
				// reactor.netty.resources
				"reactor.netty.resources.NettyBlockHoundIntegration",

				// serialVersionUID removed but not matches new default serialVersionUID
				// reactor.netty
				"reactor.netty.ChannelBindException",
				// reactor.netty.channel
				"reactor.netty.channel.AbortedException",
				// reactor.netty.http.client
				"reactor.netty.http.client.PrematureCloseException"
		]
		onlyIf {"$compatibleVersion" != "SKIP"}
  }
  tasks.japicmp.dependsOn(downloadBaseline)
  shadowJar.finalizedBy(japicmp)

  check.dependsOn jacocoTestReport

  shadowJar {
	  configure {
		  it.convention.plugins.bundle = new BundleTaskConvention(it)
		  doLast {
			  buildBundle()
		  }
	  }
	  bnd(bndOptions)

	  classifier = null

	  dependsOn(project.tasks.jar)

	  manifest {
		  inheritFrom project.tasks.jar.manifest
	  }

	  configurations = [project.configurations.shaded]

	  project.afterEvaluate {
		  dependencies {
			  def shadedDependencies = project.configurations
					  .shaded
					  .dependencies
					  .collect { "${it.group}:${it.name}".toString() }
					  .toSet()

			  // Exclude every compile-scoped dependency (including the transitive ones)
			  for (id in project.configurations.compile.resolvedConfiguration.resolvedArtifacts*.moduleVersion*.id) {
				  def module = "${id.group}:${id.name}".toString()
				  if (!shadedDependencies.contains(module)) {
					  project.configurations.shaded.exclude(group: id.group, module: id.name)
					  exclude(dependency(module))
				  }
			  }
		  }
	  }

	  exclude 'META-INF/maven*/**'
  }

  task relocateShadowJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation) {
	  target = tasks.shadowJar
	  prefix = "reactor.netty.internal.shaded"
  }

  tasks.shadowJar.dependsOn tasks.relocateShadowJar

  //add shadowJar to the publication
  publishing.publications.mavenJava.artifact(shadowJar)

  task jarFileTest(type: Test) {
	  testClassesDirs = sourceSets.jarFileTest.output.classesDirs
	  classpath = sourceSets.jarFileTest.runtimeClasspath

	  systemProperty("jarFile", shadowJar.outputs.files.singleFile)

	  dependsOn(shadowJar)
  }
  project.tasks.check.dependsOn(jarFileTest)

  project.tasks.compileTestJava.classpath += configurations.shaded
  project.tasks.test.classpath += configurations.shaded

  task shadedJarTest(type: Test) {
	  testClassesDirs = sourceSets.test.output.classesDirs

	  Set<? super File> mainOutputs = [
			  project.sourceSets.main.output.resourcesDir,
			  project.sourceSets.main.java.outputDir,
	  ]

	  classpath = shadowJar.outputs.files
	  // Exclude main outputs since we have the shaded JAR on the classpath already
	  classpath += sourceSets.test.runtimeClasspath.filter { !(it in mainOutputs) }

	  //The imports are not relocated, we do relocation only for the main sources not the tests
	  exclude '**/*PooledConnectionProviderTest*.*'

	  dependsOn(shadowJar)
  }
  project.tasks.check.dependsOn(shadedJarTest)
}

configurations.all {
  // check for updates every build
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
