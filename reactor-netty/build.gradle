/*
 * Copyright (c) 2020-2024 VMware, Inc. or its affiliates, All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import java.nio.file.Files

apply plugin: 'io.spring.javadoc-aggregate'

dependencies {
	api project(':reactor-netty-core')
	api project(':reactor-netty-http')

	// JSR-305 annotations
	compileOnly "com.google.code.findbugs:jsr305:$jsr305Version"

	compileOnly "io.micrometer:micrometer-core:$micrometerVersion"
	compileOnly "io.micrometer:micrometer-tracing:$micrometerTracingVersion"
	compileOnly "io.micrometer:context-propagation:$contextPropagationVersion"
	compileOnly "io.netty:netty-codec-haproxy:$nettyVersion"
	compileOnly "io.netty:netty-transport-native-kqueue:$nettyVersion"
	compileOnly "io.netty.incubator:netty-incubator-transport-native-io_uring:$nettyIoUringVersion"
	compileOnly "io.netty.incubator:netty-incubator-codec-http3:$nettyHttp3Version"
	compileOnly "io.projectreactor.addons:reactor-pool:$reactorPoolVersion"
}

jar {
	manifest {
		attributes("Automatic-Module-Name": "reactor.netty")
	}
}

description = "Reactor Netty with all modules"

task docsZip(type: Zip) {
	group = 'Distribution'
	archiveClassifier = 'docs'
	duplicatesStrategy "fail"
	def docsDir = file('../docs/build/site')

	if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
		logger.lifecycle("Adding dependency on :docs:antora (JDK version is >= 17)")
		dependsOn(':docs:antora')
	}
	else if (!docsDir.exists() || !Files.list(docsDir.toPath()).findFirst().isPresent()) {
		logger.lifecycle("Docs directory does not exist and JDK version is lower than 17. Skipping docsZip task.")
		enabled = false
	}

	def isSnapshot = project.version.endsWith('-SNAPSHOT')
	def version = isSnapshot ? project.version.takeWhile { it != '-' } : project.version
	boolean forcePdf = project.hasProperty('forcePdf')

	from ('../docs/build/site') {
		into 'docs'
	}

	if (!isSnapshot || forcePdf) {
		def pdfFile = file("../docs/build/assembler/reactor-netty/${version}/reactor-netty-reference-guide.pdf")
		logger.lifecycle("${pdfFile} will be included in docs zip")
		from(pdfFile) {
			rename { fileName ->
				"docs/reactor-netty-reference-guide-${project.version}.pdf"
			}
		}
	}
}

javadoc.dependsOn(aggregateJavadoc)

//add docs.zip to the publication
publishing.publications.mavenJava.artifact(docsZip)

// If current JDK version is lower than JDK17, report errors in case user invokes documentation related tasks
if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
	task antora {
		doLast {
			throw new GradleException("antora task requires JDK 17 or higher.")
		}
	}
}